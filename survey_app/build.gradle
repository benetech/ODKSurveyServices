/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

if ((new File(gradle.local)).exists()) {
    apply from: gradle.local
} else {
    apply from: gradle.remote
}

applicationScripts.each {
    apply from: it
}

apply plugin: 'com.android.application'

android {
    // The following are set in the common.gradle file
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId(groupId + surveyName)
        minSdkVersion 18
        targetSdkVersion 25
        versionCode(releaseVersionCode)
        versionName(versionCodeName)
        multiDexEnabled true
    }

    productFlavors {
        demo {
        }

        master {
        }

        snapshot {
        }

        uitest {
        }
    }

    signingConfigs {
        release {
            if (shouldSign) {
                storeFile file(signStoreFilePath)
                storePassword signStorePass
                keyAlias signKeyAlias
                keyPassword signKeyPass
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    buildTypes {
        release {
            minifyEnabled(minify)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            if (shouldSign) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable(true)
            //testCoverageEnabled(testCoverage) // Allows AndroidTest JaCoCo reports to be generated
            jackOptions {
                enabled false
            }
        }
    }

    packagingOptions {
        // Pick first occurrence of any files that cause conflicts, as defined in common.gradle
        packageList.each {
            pickFirst it
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    dexOptions {
        javaMaxHeapSize "2048M"
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
        force 'com.android.support:support-annotations:25.1.1'
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

dependencies {
    // Testing dependencies
    androidTestCompile fileTree(include: '*.jar', dir: 'libs')
    compile files('libs/aggregate-rest-interface-2016-07-21.jar')
    compile files('libs/commons-io-2.4.jar')
    compile files('libs/commons-lang3-3.1.jar')
    compile files('libs/jackson-annotations-2.4.1.jar')
    compile files('libs/jackson-databind-2.4.1.3.jar')
    compile files('libs/jackson-core-2.4.1.1.jar')
    compile files('libs/joda-time-2.9.4.jar')
    compile files('libs/slf4j-api-1.7.12.jar')
    compile files('libs/httpclientandroidlib-4.5.2-1.jar')
    compile files('libs/commons-fileupload-1.3.1.jar')
    compile files('libs/gson-2.8.0.jar')
    compile files('libs/jsr311-api-1.1.1.jar')
    compile files('libs/slf4j-api-1.7.12.jar')
    compile files('libs/jacocoagent.jar')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-core-utils:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    testCompile 'joda-time:joda-time:2.2'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'joda-time:joda-time:2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}
